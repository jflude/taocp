* EXAMPLE: FLOATING POINT CALCULATIONS (SECTION 4.2.1)
* PROGRAM A - ADDITION, SUBTRACTION AND NORMALIZATION
           ORIG 2000
BYTE       EQU  1(4:4)
EXP        EQU  1:1
FSUB       STA  TEMP
           LDAN TEMP
FADD       STJ  EXITF
           JOV  OFLO
           STA  TEMP
           LDX  ACC
           CMPA ACC(EXP)
           JGE  1F
           STX  FU(0:4)
           LD2  ACC(EXP)
           STA  FV(0:4)
           LD1N TEMP(EXP)
           JMP  4F
1H         STA  FU(0:4)
           LD2  TEMP(EXP)
           STX  FV(0:4)
           LD1N ACC(EXP)
4H         INC1 0,2
5H         LDA  FV
           ENTX 0
           SRAX 0,1
6H         ADD  FU
           JOV  N4
           JXZ  NORM
           LD1  FV(0:1)
           JAP  1F
           J1N  N2
           JMP  2F
1H         J1P  N2
2H         SRC  5
           DECX 1
           STA  TEMP
           STA  HALF(0:0)
           LDAN TEMP
           ADD  HALF
           ADD  HALF
           SRC  5
           JMP  N2
HALF       CON  1//2
FU         CON  0
FV         CON  0
NORM       JAZ  ZRO
N2         CMPA =0=(1:1)
           JNE  N5
N3         SLAX 1
           DEC2 1
           JMP  N2
N4         ENTX 1
           SRC  1
           INC2 1
N5         CMPA =BYTE/2=(5:5)
           JL   N6
           JG   5F
           JXNZ 5F
           STA  TEMP
           LDX  TEMP(4:4)
           JXO  N6
5H         STA  *+1(0:0)
           INCA BYTE
           JOV  N4
N6         J2N  EXPUN
N7         ENTX 0,2
           SRC  1
ZRO        DEC2 BYTE
8H         STA  ACC
EXITF      J2N  *
EXPOV      HLT  2
EXPUN      HLT  1
ACC        CON  0
* PROGRAM M - MULTIPLICATION AND DIVISION
Q          EQU  BYTE/2
FMUL       STJ  EXITF
           JOV  OFLO
           STA  TEMP
           LDX  ACC
           STX  FU(0:4)
           LD1  TEMP(EXP)
           LD2  ACC(EXP)
           INC2 -Q,1
           SLA  1
           MUL  FU
           JMP  NORM
FDIV       STJ  EXITF
           JOV  OFLO
           STA  TEMP
           STA  FV(0:4)
           LD1  TEMP(EXP)
           LD2  ACC(EXP)
           DEC2 -Q,1
           ENTX 0
           LDA  ACC
           SLA  1
           CMPA FV(1:5)
           JL   *+3
           SRA  1
           INC2 1
           DIV  FV
           JNOV NORM
DVZERO     HLT  3
OFLO       HLT  99
* FIXED TO FLOATING POINT
FLOT       STJ  EXITF
           JOV  OFLO
           ENT2 Q+5
           ENTX 0
           JMP  NORM
* FLOATING POINT TO FIXED
FIX        STJ  9F
           STA  TEMP
           LD1  TEMP(EXP)
           SLA  1
           JAZ  9F
           DEC1 1
           CMPA =0=(1:1)
           JE   *-4
           ENN1 -Q-4,1
           J1N  EXPOV
           ENTX 0
           SRAX 0,1
           CMPX =1//2=
           JL   9F
           JG   *+2
           JAO  9F
           STA  *+1(0:0)
           INCA 1
9H         JMP  *
* FLOATING POINT COMPARISON
FCMP       STJ  9F
           JOV  OFLO
           STA  TEMP
           LDAN TEMP
           LDX  ACC
           CMPA ACC(EXP)
           JGE  1F
           STX  FU(0:4)
           LD2  ACC(EXP)
           STA  FV(0:4)
           LD1N TEMP(EXP)
           JMP  4F
1H         STA  FU(0:4)
           LD2  TEMP(EXP)
           STX  FV(0:4)
           LD1N ACC(EXP)
4H         INC1 0,2
5H         LDA  FV
           LDX  FV(0:0)
           DEC1 5
           J1N  *+2
           ENT1 0
           SRAX 5,1
           ADD  FU
           JOV  7F
           CMPA EPSILON(1:5)
           JG   8F
           JL   6F
           JXZ  9F
           JXP  1F
           JAP  9F
           JMP  8F
7H         ENTX 1
           SRC  1
           JMP  8F
1H         JAP  8F
6H         ENTA 0
8H         CMPA =0=
9H         JMP  *
EPSILON    CON  1
* TEST PROGRAM
           ORIG 3000
START      LDA  REG
           STA  ACC
           LDA  ARG
           JMP  FSUB
           HLT  *+1
REG        CON  1(0:2),34(1:1)
ARG        CON  1(0:2),33(1:1)
           END  START
