* TRACE ROUTINE (SECTION 1.4.3.2)
           ORIG *+99
BUF        CON  0
ENTER      STX  TEST(0:2)
           STX  LEAVEX(0:2)
           STA  AREG
           ST1  I1REG(0:2)
           STJ  JREG
           LDA  JREG(0:2)
CYCLE      STA  PREG(0:2)
PTR        ENT1 -100
           JBUS *(0)
           STA  BUF+1,1(0:2)
TEST       DECA *
           JAZ  LEAVE
PREG       LDA  *
           STA  INST
           STA  BUF+2,1
           SRA  2
           STA  INST1(0:3)
           LDA  AREG
           STA  BUF+3,1
           LDA  I1REG(0:2)
           STA  BUF+4,1
           ST2  BUF+5,1
           ST3  BUF+6,1
           ST4  BUF+7,1
           ST5  BUF+8,1
           ST6  BUF+9,1
           STX  BUF+10,1
           LDA  JREG(0:2)
           STA  BUF+1,1(4:5)
           ENTA 8
           JNOV 1F
           ADD  BIG
1H         JL   1F
           INCA 1
           JE   1F
           INCA 1
1H         STA  BUF+1,1(3:3)
           INC1 10
           J1N  1F
           OUT  BUF-99(0)
           ENT1 -100
1H         ST1  PTR(0:2)
           LDA  INST(5:5)
           DECA 38
           JANN 1F
           INCA 6
           JANZ 2F
           LDA  INST(0:4)
           STA  *+2(0:4)
JREG       LDA  *
           STA  *
           JMP  INCP
2H         DECA 2
           JANZ 2F
           JMP  3F
1H         DECA 9
           JAP  2F
3H         LDA  8F(0:3)
           STA  INST(0:3)
2H         LDA  AREG
I1REG      ENT1 *
* ALL REGISTERS EXCEPT J NOW HAVE PROPER VALUES WITH RESPECT
* TO THE EXTERNAL PROGRAM.
INST       NOP  *
           STA  AREG
           ST1  I1REG(0:2)
INCP       LDA  PREG(0:2)
           INCA 1
           JMP  CYCLE
8H         JSJ  JUMP
JUMP       LDA  8B(4:5)
           SUB  INST(4:5)
           JAZ  *+4
           LDA  PREG(0:2)
           INCA 1
           STA  JREG(0:2)
INST1      ENTA *
           JMP  CYCLE
LEAVE      LDA  AREG
           LD1  I1REG(0:2)
LEAVEX     JMP  *
AREG       CON  0
B4         EQU  1(1:1)
BIG        CON  B4-8,B4-1(1:1)
